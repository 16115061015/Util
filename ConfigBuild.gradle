project.ext {
    applicationId = "com.hzy.cnn.CustomView"
//是否允许module单独调试
    isModuleDebug = false

    //根据Module配置
    ApplicationConfig = {
        extension ->
            extension.apply plugin: 'com.android.application'
            extension.description "app"
            DefaultPlugin()
            DefaultAndroidConfig()
            extension.android.defaultConfig {
                applicationId project.ext.applicationId
            }
            DefaultKotlinConfig()
            DefaultDependencies()

            extension.android.sourceSets {
                main {
                    manifest.srcFile 'src/main/AndroidManifest.xml'
                }
            }

    }

    //根据Module配置
    LibConfig = {
        extension ->
            if (isModuleDebug) {
                extension.apply plugin: 'com.android.application'
                extension.description "app"
            } else {
                extension.apply plugin: 'com.android.library'
                extension.description "lib"
            }
            DefaultPlugin()
            DefaultAndroidConfig()
            DefaultKotlinConfig()
            DefaultDependencies()
            //设置清单文件
            extension.android.sourceSets {
                main {
                    if (project.ext.isModuleDebug) {
                        //需要手动创建文件夹配置不同的清单文件，这个文件应该包含activity启动项
                        manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
                    }else{
                        //这个文件不能有启动项否则启动时会有多个app
                        manifest.srcFile 'src/main/AndroidManifest.xml'
                    }
                }
            }

    }


    DefaultPlugin = {
        apply plugin: 'kotlin-android'
        apply plugin: 'kotlin-android-extensions'
        apply plugin: 'kotlin-kapt'
    }
    //传入project.android配置
    DefaultAndroidConfig = {
        android.compileSdkVersion = 29
        android.defaultConfig {
            minSdkVersion 21
            targetSdkVersion 29
            versionCode 1
            versionName "1.0"
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            //方法数限制
            multiDexEnabled true
            //ARouter
            javaCompileOptions {
                annotationProcessorOptions {
                    arguments = [moduleName: project.getName()]
                }
            }
        }
        android.compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }


    DefaultKotlinConfig = {
        kotlin.experimental {
            coroutines 'enable'
        }
    }

    DefaultDependencies = {
        dependencies {
            implementation fileTree(dir: 'libs', include: ['*.jar'])
            //通用库
            implementation 'androidx.appcompat:appcompat:1.1.0'
            implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
            testImplementation 'junit:junit:4.13'
            androidTestImplementation 'androidx.test.ext:junit:1.1.1'
            androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
            implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
            implementation 'androidx.recyclerview:recyclerview:1.1.0'
            implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.4'
            implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.4'

            //BVH
            api 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.46'

            //lifecycle协程库
            implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
            implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.0-alpha02"
            implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"

            //Fresco
            implementation("com.facebook.fresco:fresco:2.1.0")
            implementation("com.facebook.fresco:animated-gif:2.1.0")
            implementation("com.facebook.fresco:animated-webp:2.1.0")
            implementation("com.facebook.fresco:webpsupport:2.1.0")
            implementation("com.facebook.fresco:imagepipeline-okhttp3:2.1.0") {
                exclude module: "okhttp"
            }

            //ARouter
            api 'com.alibaba:arouter-api:1.4.1'
            kapt 'com.alibaba:arouter-compiler:1.1.4'
        }
    }


}